set terminal epslatex dashed
#set terminal pngcairo

set xlabel "Time (minutes:seconds)"
set ylabel "Requests per second"
set yrange[0:3000]

set xdata time
set timefmt "%s"
set format x "%M:%S" 

set key top right box  height 0.8 width 2.5 spacing 1.3


set style function linespoints
set style line 1 lw 3 lt 1 ps 2 pt 3 lc rgb "#42443C" pointinterval 10
set style line 2 lw 3 lt 1 ps 2 pt 4 lc rgb "#C5172C" pointinterval 10
set style line 3 lw 3 lt 1 ps 2 pt 4 lc rgb "black" pointinterval 10
set style line 4 lw 5 lt 1 ps 2 pt 4 lc rgb "#63A52C" 

set label "Restrain"  at '1850', 2000
set arrow 1 from first '1850',0 to '1850', 2000 nohead ls 3 lw 2 front size 30, 15 



set output 'serial.tex'
plot 'data_serial.txt' using 1:2 with line title 'serial replay' ls 1 , \
	 '' using 1:3 with line title 'concurrent client' ls 2



set output 'clustered.tex'
plot 'data_clustered.txt' using 1:2 with line title 'clustered replay' ls 1 , \
	 '' using 1:3 with line title 'concurrent client' ls 2	 


set terminal pdf font "Klavika,17"
set xtics 180
set output 'clustered_presentation.pdf'
replot

set terminal epslatex solid


########################################################################################
set style line 1 lw 5 lt -1 ps 4 pt 2 lc "black" pointinterval 20
set style line 2 lw 5 lt 1  ps 4 pt 4 lc "black" pointinterval 20
set key top left box  height 0.8 width 2 spacing 1.3


set yrange[0:2700]




##############
set output 'serial_paper.tex'	
set xtics 600
unset label
set label "Restrain"  at '1850',1500
set arrow 1 from first '1850',0 to '1850', 1500 nohead ls 3 lw 2 front size 30, 15 

plot 'data_serial.txt' using 1:2 smooth bezier with linespoints title 'serial' ls 1 , \
	 '' using 1:3 smooth bezier with linespoints title 'client'  ls 1 lt -1 pt 7	 


################
set output 'clustered_paper.tex'	
set xrange ['0':'635']
set xtics 180
unset label
set label "Restrain"  at '450',2500
set arrow 1 from first '590',0 to '590', 2500 nohead ls 3 lw 2 front size 30, 15 

plot 'data_clustered.txt' using 1:2 smooth bezier with linespoints title 'clustered' ls 2 lt 0 pt 2, \
	 '' using 1:3 smooth bezier with linespoints title 'client' ls 1 lt -1 pt 7	 
